# Kukicha Standard Library - MCP Server Wrapper

petiole mcp

import "stdlib/ctx" as ctxpkg
import "github.com/modelcontextprotocol/go-sdk/mcp"

# ToolHandler is a callback for handling MCP tool invocations.
type ToolHandler func(map of string to any) (any, error)

# SchemaProperty represents a property in a JSON schema
type SchemaProperty
    Name string
    Type string
    Description string

# New creates a new MCP server with the given name and version
func New(name string, version string) reference mcp.Server
    return mcp.NewServer(reference of mcp.Implementation{
        Name: name,
        Version: version,
    }, empty)

# Serve runs the server on the stdio transport (blocking)
func Serve(server reference mcp.Server) error
    bg := ctxpkg.Background()
    return server.Run(ctxpkg.Value(bg), reference of mcp.StdioTransport{})

# Prop creates a new SchemaProperty
func Prop(name string, typ string, description string) SchemaProperty
    return SchemaProperty{Name: name, Type: typ, Description: description}

# Schema creates a JSON schema object from a list of properties
func Schema(props list of SchemaProperty) map of string to any
    properties := make(map of string to any)
    for prop in props
        properties[prop.Name] = map of string to any{
            "type": prop.Type,
            "description": prop.Description,
        }

    return map of string to any{
        "type": "object",
        "properties": properties,
    }

# Required adds a list of required property names to a schema
func Required(schema any, names list of string) any
    switch schema as s
        when map of string to any
            s["required"] = names
            return s
        otherwise
            return schema

# TextResult creates a successful tool result containing the given text.
func TextResult(text string) any
    return reference of mcp.CallToolResult{
        Content: list of mcp.Content{reference of mcp.TextContent{Text: text}},
    }

# ErrorResult creates an error tool result containing the given message.
func ErrorResult(msg string) any
    return reference of mcp.CallToolResult{
        Content: list of mcp.Content{reference of mcp.TextContent{Text: msg}},
        IsError: true,
    }
