# target: mcp

import "stdlib/mcp"
import "stdlib/cast"

# A tool that adds two numbers, demonstrating smart type conversion
# Using 'any' return type to match mcp.ToolHandler: func(map of string to any) (any, error)
func add(args map of string to any) (any, error)
    # LLMs often send numbers as strings, cast.SmartInt handles both
    a := args["a"] |> cast.SmartInt() onerr 0
    b := args["b"] |> cast.SmartInt() onerr 0
    
    result := a + b
    res := "{a} + {b} = {result}"
    return res as any, empty

func main()
    # Create the server
    server := mcp.New("Kukicha Example", "1.0.0")

    # Define the schema for the 'add' tool
    props := list of mcp.SchemaProperty{mcp.Prop("a", "number", "The first number"), mcp.Prop("b", "number", "The second number")}
    addSchema := mcp.Schema(props)
    addSchema = mcp.Required(addSchema, list of string{"a", "b"}).(map of string to any)

    # Register the tool
    server |> mcp.Tool("add", "Add two numbers", addSchema, add)

    # Print to stdout (will be redirected to stderr by compiler because of # target: mcp)
    print("MCP Server starting...")

    # Serve on stdio (blocking)
    mcp.Serve(server) onerr
        print("Server finished with error: {error}")
